# [#0001] - Modules

|             |      |
| ----------- | ---- |
| **Authors** | Quil |

- [ ] Discussion
- [ ] Implementation

## Summary

Programs primarily manipulate data, so a programming language needs good support not only for modelling processes, but also the data these processes manipulate.

For Origami, we're interested in modelling data structures that:

- Support evolution. That is, we must support defining and extending data structure definitions **without** breaking existing code. This rules out positional values and tagged unions (as commonly implemented).

- Support precise modelling. So we must support scalar types, aggregate types, and one-of types.

- Support serialisation and parsing.

- Support encapsulation with fine-grained capabilities. It should be possible to provide a read-only access to a single field in a data structure to someone without giving them access to the whole data structure. Same for write-only accesses.

This document describes how Origami achieves these goals.

## References

- [Extensible records with scoped labels](http://www.cs.ioc.ee/tfp-icfp-gpce05/tfp-proc/21num.pdf)
  -- Dan Leijen, 2005

- [Cap'n Proto language specification](https://capnproto.org/language.html)
  -- Kenton Varda

## Core language

The short story:

```
x in Variables
s in Symbols
v in Values

Label l := x | s

Values += {}

Expression e :=
  | { e with l_1: v_1, ..., l_n: v_n }      -- introduction
  | e.l                                     -- projection
  | symbol x                                -- introducing symbols
  | x | v
```

Records are a collection of labelled values. A label may be either a variable name (a regular name in a flat namespace), or a symbol (an unforgeable, unique identifier).

They may be constructed either with the special empty record value, or by extending an existing record with new labelled values.

Values may be accessed by projecting them through its label.

The following semantics describe this less informally:

```
E[{ { l1_1: v1_1, ..., l1_n: v1_n } with { l2_1: v2_1, ..., l2_m: v2_m }}]
--> { l1_1: v1_1, ..., l1_n: v1_n, l2_1: v2_1, ..., l2_m: v2_m }

E[{ l: v, ... }.l]
--> v

E[symbol x]
--> s, where `s` is an unique symbol with a textual description `x`
```

## Structuring data

There are two primary forms of data that applications manipulate:

- Scalar data: a single thing like a piece of text, or a number;
- Aggregate data: a collection of different kinds of data. For exemple, information about a person including their name, age, and address;

Applications also manipulate these pieces of information differently depending on the context. A context may expect data that may be a Person information or a Company information. It may expect data that may exist or not. We separate the _usage_ of data from the _modelling_ of data.

For modelling data, Origami provides records. Records are a collection of key/value pairs, such as:

```
let alice = { name: "Alice", age: 12 };
```

Records give us a way of bringing different pieces of information together, and passing them around as a single thing.

We may use this data in different ways. The common ones are by projecting a field from the record:

```
alice.name; // => "Alice"
```

Which works as long as the record defines such field. Or using pattern matching as a way of projecting many fields at once:

```
match alice with {
  case { name, age } -> // handle name and age here
}
```

Which works as long as the record defines all required fields.

## Records

As previously said, a record is a collection of labelled values. The key (or label) for these values can be a simple name (for example, `name` or `age`), as in our previous `alice` example. But it can also be an unique symbol. We'll see unique symbols in more details later when we cover encapsulation, capabilities, and security.

For now, let's focus on records.

We may construct a record by listing their labelled values:

```
let point2d = { x: 1, y: 2 };
let point3d = { x: 1, y: 2, z: 0 };
```

We may also construct a record by extending an existing record:

```
// this is equivalent to our previous `point3d` definition
let point3d' = { point2d with z: 0 };
```

In fact, our short `{ l: v, ... }` form is sugar for extending the empty record, so the following are strictly equivalent:

```
let p1 = { x: 1, y: 2 };
let p2 = { {} with x: 1, y: 2 };
```

Separating how data is defined and constructed from hwo data is used, and placing the data constraints on the latter allows data to evolve without breaking programs. As long as the data continues to provide the same labelled values with the same semantics, all users of the data will continue to work. The producers of the data are free to provide any additional labelled values they want, at any point in time.

### Extending records

Records can be extended with the `with` syntax. Conceptually `{ e with ... }` means "construct a record that has the same labelled values in `e`, and these labelled values". Like in extensible records, a new value with the same label as an existing labelled value overrides that value.

The literal interpretation of these semantics isn't efficient, however. It would require allocating memory for the entire set of labelled values, rather than just the ones that are different. Since records are effectively immutable, however, this structure can be safely shared between different records, reducing the costs of constructing new records (both in time and memory) to the differences between them.

While extending records, new labels may be added, or old labels may be assigned a new value, but labels cannot be removed from the record (although they may be assigned a value that suggests their non-existence, such as `Nothing`).

### Names and symbols

Labels may be a plain name or a symbol. We've seen labels as plain names so far:

```
let point2d = { x: 1, y: 2 };
```

In this expression, `x` and `y` are plain names. They're available for everyone to use, anytime, without needing to do anything special to access them. Their _names_ are already known.

But values may also be labelled by symbols. A symbol is a special form of label whose name is _unique_. You may construct a symbol like so:

```
symbol tag ("The type of a value.");
```

The symbol is referred by the variable `tag`, and has the (optional) description "The type of a value".

We may then use this symbol to label values and project them:

```
let point2d = { (tag): "point2d", x: 1, y: 2 };
point2d.(tag) --> "point2d";
```

The parenthesised form where a label is expected allows one to use an expression that resolves to a symbol. And that symbol is then used to label information in the record, or project information from it.

It's important to note that the **only** way of accessing and associating information with a symbol is by using a reference to it. Such reference is unforgeable: you can only get your hands in one if the code that created it passes a reference to you.

Further, labels that use symbols are not shown when inspecting an object, serialising it, etc. Unless the code has an explicit reference that allows such value to be exposed.

### Methods

As in prototype-based OOP, records double as objects. Here the labelled values play the same role as prototype-based OOP's slots, and function-valued labels are treated as methods. A simple method could be defined as follows:

```
let counter = (value) => {
  value: value,
  next: () => counter(value + 1)
};

let c1 = counter(0);
let c2 = c1.next();
let c3 = c2.next();
c3.value --> 2;
```

To support open recursion, Origami provides a special method syntax. So counter could be expressed like this:

```
let counter = {
  value: 0,
  member self.next() = { self with value: self.value + 1 }
};

counter.next().next() --> 2;
```

Here the parameter `self` refers to the receiver parameter of the method (like in F#, you can name it anything). New counters are constructed by extending the receiver with a new value, incrementing the receiver's value.

Projecting a method partially specifies the method's receiver, resulting in a regular function. You may think of methods as a sort-of curried function, where one of the parameters is specified on the left side of the dot. This way:

```
let next = counter.next;
next().next() --> 2;
```

The following table lists all special syntaxes that Origami defines for methods.

| **Syntax**             | **Description**                              | **Label**             |
| ---------------------- | -------------------------------------------- | --------------------- |
| `member self.x`        | A proxy for the projection of `x`            | `x`                   |
| `member self.f(...)`   | A regular method                             | `f`                   |
| `member self.x <- y`   | Updates the value of `x`                     | `op$set_x`            |
| ---------------------- | -------------------------------------------- | --------------------- |
| **Collections**        |                                              |                       |
| `member self[k]`       | Retrieves the value indexed by `k` in `self` | `op$at`               |
| `member self[k] <- y`  | Updates the value indexed by `k` in `self`   | `op$at_put`           |
| `member value in self` | Membership of `value` in `self`              | `op$in`               |
| `member self ++ that`  | Concatenation of collections                 | `op$concat`           |
| ---------------------- | -------------------------------------------- | --------------------- |
| **Logical**            |                                              |                       |
| `member not self`      | Logical negation                             | `op$not`              |
| `member self and that` | Logical conjunction                          | `op$and`              |
| `member self or that`  | Logical disjunction                          | `op$or`               |
| ---------------------- | -------------------------------------------- | --------------------- |
| **Relational**         |                                              |                       |
| `member self == that`  | Structural (value) equality                  | `op$equal`            |
| `member self /= that`  | Structural (value) inequality                | `op$not_equal`        |
| `member self > that`   | Greater than                                 | `op$greater_than`     |
| `member self >= that`  | Greater than or equal to                     | `op$greater_or_equal` |
| `member self < that`   | Less than                                    | `op$less_than`        |
| `member self <= that`  | Less than or equal to                        | `op$less_or_equal`    |
| ---------------------- | -------------------------------------------- | --------------------- |
| **Arithmetic**         |                                              |                       |
| `member self + that`   | Arithmetic addition                          | `op$plus`             |
| `member self - that`   | Arithmetic subtraction                       | `op$minus`            |
| `member self * that`   | Arithmetic multiplication                    | `op$times`            |
| `member self / that`   | Arithmetic division                          | `op$divide`           |
| `member self ** that`  | Arithmetic exponentiation                    | `op$power`            |
| ---------------------- | -------------------------------------------- | --------------------- |
| **Categories**         |                                              |                       |
| `member self >> that`  | Composition of morphisms (left-to-right)     | `op$compose_right`    |
| `member self << that`  | Composition of morphisms (right-to-left)     | `op$compose_left`     |

> **TODO**: operators do need some love right now. Will arithmetic ones be based on abstract algebra?

---

## Records

In principle, Origami only has _record_ types. And they're anonymous and extensible. For example:

```
let point2d = { x: 1, y: 2 };
```

Constructs an anonymous record containing two labels, `x` and `y`, whose associated values are `1` and `2`. We could construct a new point based on this one:

```
let point3d = { point2d with z: 0 };
```

This constructs a point that has the same properties as the one referenced by `point2d`, plus the property `z`. The additional properties can also be used to override labels in the new record:

```
let point3d' = { point3d with z: 2 };
```

This constructs a new point that has `z = 2`, but the old `point3d` value is not changed. New records efficiently share the properties of the old ones, so the instantiation cost is proportional to the number of new/overriden properties, rather than all of the properties.

### Using records

We may use any value associated with a label by projecting it. For example:

```
let xyz = point3d.x + point3d.y + point3d.z;
```

We may access the values through pattern matching. For example:

```
let xyz = match point3d {
  case { x: a, y: b, z: c } -> a + b + c;
  // or as a short form:
  case { x, y, z } -> x + y + z;
}
```

Pattern matching also allows matching only parts of the record:

```
let xy = match point3d {
  case { x, y } -> x + y;
}
```

### Methods

Besides data, records also support object-oriented programming (through prototypes). These are defined with the `member` form. For example, a more complete point2d may be defined as:

```
let point2d_base = {
  member self.distance(aPoint) = match [self, aPoint] {
    case [{x1, y1}, { x2, y2 }] ->
      (((x2 - x1) ** 2) + ((y2 - y1) ** 2)).square_root();
  }
};

let p1 = { point2d_base with x: 1, y: 2 };
let p2 = { point2d_base with x: 3, y: 5 };

p1.distance(p2);
```

The familiar extension syntax (`{ base with new_props }`) works here to reuse methods defined in another record. This is a delegative form of inheritance, and no data is copied in memory--thus instantiations are both time and memory efficient.

### Type definitions

A record type may be defined that includes commonly used features. Type definitions fulfill the same role as classes in most object-oriented languages, and then some more. Unlike most class formulations, however, type definitions do not support inheritance.

Point2d could alternatively be defined as:

```
data Point2d {
  public x;
  public y;

  member self.distance(aPoint) = match [self, aPoint] {
    case [{ x1, y1 }, { x2, y2 }] ->
      (((x2 - x1) ** 2) + ((y2 - y1) ** 2)).square_root();
  }

}
```

Points are then constructed through a different form:

```
let p1 = Point2d { x: 1, y: 2 };
let p2 = Point2d { x: 3, y: 5 };
```

This both enforces that all fields that the type expect are provided, and inherits all of the methods from `Point2d`.

### Capabilities

The record form provides a way of constructing simple, extensible records, but does not give us anything to control who gets to access which pieces of data, and what they can do with it.

Capabilities have to be declared as record types in the module:

```
data Point2d { private x; private y }
```

This declares a record type with two fields. But these fields cannot be constructed by regular code. The following is not a valid point2d:

```
let p = { x: 1, y: 2 };
```

To construct a valid point2d, one must get a capability for constructing points. When declaring points in the module, the declaring name will contain all capabilities, so a point may be constructed as follows:

```
let point2d = Point2d { x: 1, y: 2 };
```

This uses the `constructing capability` in Point2d, and constructs a point with private fields. These fields cannot be projected in the regular way either, so the following fails, as there's no _publicly-accessible_ fields `x` or `y`:

```
point2d.x + point2d.y
// [TypeError] `x` is not a public field in point2d
```

So we also need a projection capability, which is again provided by the Point2d namespace:

```
Point2d.x(point2d) + Point2d.y(point2d);
```

Likewise, in order to pattern match, we must use the Point2d capability:

```
match point2d {
  case Point2d { x, y } -> x + y;
}
```

Finally, we can promote the record to a public one, with the Point2d capability:

```
let p = Point2d.promote(point2d);
p.x + p.y;
```

Or even lift a regular record into a Point2d one:

```
let p = Point2d.lift({ x: 1, y: 2 });
p.x;
// [TypeError] `x` is not a public field in p
```

### Granting fine-grained capabilities

Of course one may pass the entire `Point2d` capability around to allow other people to access and construct values of `Point2d`. But that's not always desirable. Sometimes you may be interested in only giving away the capability of _constructing_ values of `Point2d`, but not reading them. Or you may want to allow reading some fields, but not construct new values of the type or read other fields.

Record capabilities in Origami are just records with specific methods, as shown in the table below:

| **Capability**       | **Method**                     |
| -------------------- | ------------------------------ |
| Constructing records | `member self { ... }`          |
| Projecting a field   | `member self.<field>(<value>)` |
| Pattern matching     | `member self.unapply(<value>)` |
| Promoting            | `member self.promote(<value>)` |
| Lifting              | `member self.lift(<value>)`    |
| Type testing         | `member value is self`         |

For example, to restrict the capabilities to only constructing `Point2d` and reading the `x` field we could use:

```
define RestrictedPoint2d = {
  member self { x, y } = Point2d { x, y };

  member self.x(value) = Point2d.x(value);

  member self.unapply(value) = match value {
    case Point2d { x } -> { x };
    default -> null;
  }
}
```

We can then pass `RestrictedPoint2d` around and be sure that people with a reference to it will never be able to access the field `y`, or promote point2d records to public ones. Something like this would always fail:

```
match point2d {
  case RestrictedPoint2d { x, y } -> "ok";
  default -> "failed";
}
--> "failed";
```
