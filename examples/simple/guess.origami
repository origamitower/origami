% language: origami

import "@origami/io" exposing write_line, read;
import "@origami/random" exposing random_integer;
import "@origami/foundation" exposing integer, result, string;
import "@origami/prelude" exposing (_ < _), (_ > _);

function main(_) *{
  !write_line("Guess the number!");
  let secret_number = !random_integer(1, 101);

  while true {
    let guess = integer.try_parse(!read());

    match guess {
      case result.Error(_):
        !write_line("Please enter a valid number.");

      case result.Ok(guess):
        !write_line("You guessed %{s:guess}" |> string.format({ guess }));

        if guess < secret_number then      !write_line("Too small!")
        else if guess > secret_number then !write_line("Too big!")
        else {
          !write_line("You win");
          break;
        }
    }
  }
}